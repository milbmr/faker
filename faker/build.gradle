plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.milbmr'
version = '1.0.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'faker'
            from components.java
            pom {
                name = 'Faker'
                description = 'An up to date fork of the original java-faker you can use it in your project to generate fake data'
                url = 'https://github.com/milbmr/faker'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'barlog'
                        name = 'Miloud'
                        email = 'miloamg@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/milbmr/faker.git'
                    developerConnection = 'scm:git:ssh://github.com:milbmr/faker.git'
                    url = 'https://github.com/milbmr/faker'
                }
            }
        }
    }

    repositories {
        maven {
            name = "LocalMavenWithChecksums"
            url = uri(layout.buildDirectory.dir("staging"))
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}


dependencies {
    implementation("org.yaml:snakeyaml:2.5")
    implementation("com.github.mifmif:generex:1.0.2")
    implementation("org.apache.commons:commons-lang3:3.19.0")

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.hamcrest:hamcrest:3.0")
    testImplementation("org.mockito:mockito-core:5.20.0")
    testImplementation("commons-validator:commons-validator:1.10.0")
    testImplementation("ch.qos.logback:logback-classic:1.5.21")
    testImplementation("org.reflections:reflections:0.10.2")
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}